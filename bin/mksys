#!/usr/bin/env fish

# Parse args:
# -p or --purge: purge destination folders before copy
argparse 'p/purge' -- $argv

set R (dirname (dirname (status -f))) # Rootdir for `syscfg` project
set T ./machines/(hostname)/this.nix  # Config for this specific machine
set H $HOME/.config/nixpkgs              # Home-manager's configuration directory


function theme_get -S
    nix eval -f $R/$T theme.$argv[1]
end

function build_sys -S
    echo "Updating system configuration"

    if set -q _flag_p
        echo ". Purging /etc/nixos"
        sudo rm -r /etc/nixos/*
    end

    echo ". Copying config to /etc/nixos"
    sudo mkdir -p /etc/nixos/machine/current
    sudo cp -r $R/system/*              /etc/nixos
    sudo cp -r $R/machines/(hostname)/* /etc/nixos/machine/current/
    sudo cp -r -t /etc/nixos $R/theme $R/assets

    echo ". Rebuilding system"
    sudo nixos-rebuild -I nixpkgs=(cat $R/pinned/nixpkgs) "switch" --upgrade --show-trace \
       2>&1 | sed 's/^/.   /'
end

function build_home -S
    echo "Updating home configuration"

    echo ". Creating cache state"
    mkdir -p $HOME/.config/polybar/logs       # Polybar logs
    touch $HOME/.config/polybar/logs/main.log
    mkdir -p $HOME/dcs/mail                   # Maildir

    if set -q _flag_p
        echo ". Purging ~/.config/nixpkgs"
        rm -r $H/*
    end

    # Home manager files
    echo ". Copying home-manager config"
    mkdir -p $H/machine/current
    cp -r $R/home/*                $H/
    cp -r $R/machines/(hostname)/* $H/machine/current
    cp -r -t $H $R/theme $R/assets

    echo ". Running home-manager switch"
    home-manager "switch" --show-trace 2>&1 | sed 's/^/.   /'

    echo ". Forcing restarts"
    xmonad --restart
    feh --bg-scale (theme_get wallpaper)
    set -l themename \'(theme_get emacs-theme | string trim -c \")
    emacsclient -e  "(load-theme $themename)" > /dev/null

    # HACK: There must be a nixier way of doing this
    echo ". Write theme setting to emacs"
    echo "(setq doom-theme $themename)" > $HOME/.config/doom/my/theme.el

    # HACK: msmtp pass command doesn't use custom directory
    # FIXME: When I merge passwords into syscfg, these should be better.
    echo ". Deploying some links"
    unlink $H/.password-store > /dev/null 2>&1
    ln -s $H/dcs/secrets/passwords $H/.password-store

end

switch $argv[1]
    case home
        build_home
    case sys
        build_sys
    case '*'
        build_sys; and build_home
end
